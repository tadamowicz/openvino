# Copyright (C) 2018-2023 Intel Corporation
# SPDX-License-Identifier: Apache-2.0
#

set(TARGET_NAME ov_gna_unit_tests)

# RPATH is always enabled for unit tests
set(CMAKE_SKIP_RPATH OFF)

# because unit tests use plugins object files compiled with LTO
if(CMAKE_COMPILER_IS_GNUCXX AND CMAKE_CXX_COMPILER_VERSION VERSION_GREATER_EQUAL 9.0)
    set(CMAKE_INTERPROCEDURAL_OPTIMIZATION_RELEASE ${ENABLE_LTO})
endif()

file(GLOB MODULE_SOURCES
    "${CMAKE_CURRENT_SOURCE_DIR}/*.cpp"
    "${CMAKE_CURRENT_SOURCE_DIR}/*.hpp"
    "${CMAKE_CURRENT_SOURCE_DIR}/**/*.cpp"
    "${CMAKE_CURRENT_SOURCE_DIR}/**/*.hpp"
)

add_executable(${TARGET_NAME} ${MODULE_SOURCES})

target_link_libraries(${TARGET_NAME}
    openvino::gmock
    openvino::gtest
    openvino::gtest_main
    openvino::ngraphFunctions
    openvino::commonTestUtils
    inference_engine_legacy_s
    openvino_intel_gna_plugin_test_static
)

target_include_directories(${TARGET_NAME}
    PRIVATE ${OpenVINO_SOURCE_DIR}/src/inference/src
)

#needed in order to locate inference_engine_s.lib
target_link_directories(${TARGET_NAME}
    PRIVATE "${OpenVINO_SOURCE_DIR}/bin/intel64/$<CONFIG>"
)

if(CMAKE_CXX_COMPILER_ID STREQUAL "MSVC")
    set_target_properties(${TARGET_NAME} PROPERTIES LINK_FLAGS -IGNORE:4286)

    #FIXME the flag allows to solve linker error 2005 (Symbol already defined)
    # by taking into account first occurence of the symbol and ignoring rest of them.
    # In this case the problem ocucrs in Release version only.
    # Duplicated symbol definitions are generated by all source files include "ie_common.h" header
    # Very likely the issue is caused by IE_ASSERT definition in ie_common.h:450-452.
    target_link_options(${TARGET_NAME} PRIVATE "/FORCE")
endif()

if(SUGGEST_OVERRIDE_SUPPORTED)
    set_source_files_properties(gna_model_serial_test.cpp
                                PROPERTIES COMPILE_OPTIONS -Wno-suggest-override)
endif()

set_target_properties(${TARGET_NAME} PROPERTIES VS_DEBUGGER_ENVIRONMENT "PATH=%PATH%;${OpenVINO_SOURCE_DIR}/temp/tbb/bin;${OpenVINO_SOURCE_DIR}/${BIN_FOLDER}/$<CONFIG>")

add_test(NAME ${TARGET_NAME}_run COMMAND ${TARGET_NAME})
set_tests_properties(${TARGET_NAME}_run PROPERTIES ENVIRONMENT "PATH=${OpenVINO_SOURCE_DIR}/temp/tbb/bin")
set_tests_properties(${TARGET_NAME}_run PROPERTIES ENVIRONMENT_MODIFICATION "PATH=path_list_append:${OpenVINO_SOURCE_DIR}/${BIN_FOLDER}/$<CONFIG>")
